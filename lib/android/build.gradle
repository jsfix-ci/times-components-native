import groovy.json.JsonSlurper

// Top-level build file where you can add configuration options common to all sub-projects/modules.

apply plugin: 'base'

buildscript {

    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:7.0.4")

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

def reactRepositoryPath = "$rootDir/repo"
def hermesVersion = new JsonSlurper().parseText(file("$rootDir/../../node_modules/hermes-engine/package.json").text).version
def graphQlFilesPath = './xnative/src/main/res/raw'

allprojects {
    repositories {
        google()
        jcenter()

        maven {
            url "$reactRepositoryPath"
        }

    }
}

task removeGraphQlFiles(type: Delete) {
    delete graphQlFilesPath
}

task removeReactArchives(type: Delete) {
  delete reactRepositoryPath
}

clean.dependsOn(removeGraphQlFiles)
clean.dependsOn(removeReactArchives)
def reactSubprojects = [
    "xnative-rn-svg",
    "xnative-rn-webview",
    "xnative-rn-device-info",
    "xnative-rn-community-netinfo",
    "xnative-rn-community-art",
]

task generateReactArchives {
    subprojects { subproject ->
        apply plugin: 'maven-publish'
        def tcnPackageJson = new JsonSlurper().parseText(file("$rootDir/../../package.json").text)

        if (reactSubprojects.contains(subproject.name)) {
            task generateReactArchive {
                publishing {
                  repositories.maven {
                    url = reactRepositoryPath
                  }
                  publications {
                    ReactDependency(MavenPublication) {
                        groupId 'uk.co.thetimes'
                        artifactId subproject.name
                        version tcnPackageJson.version
                        artifact("$buildDir/outputs/aar/${subproject.name}-release.aar")
                    }
                  }
                }
            }
            generateReactArchive.dependsOn(":${subproject.name}:assembleRelease")
            publishReactDependencyPublicationToMavenRepository.dependsOn(generateReactArchive)
            generateReactArchives.dependsOn(publishReactDependencyPublicationToMavenRepository)
      }
    }

    task reactNativeCopy(type: Copy) {
        from('../../node_modules/react-native/android')
        into(reactRepositoryPath)
    }

    task hermesCopy {

        task hermesCopyArchive(type: Copy) {
          from('../../node_modules/hermes-engine/android/hermes-release.aar')
          into("$reactRepositoryPath/com/facebook/hermes/hermes-engine/$hermesVersion")
          rename('hermes-release.aar', "hermes-engine-${hermesVersion}.aar")
        }

        task generateHermesArchive {
          apply plugin: 'maven-publish'
          description "Generates pom file for Hermes Engine"
          publishing {
            publications {
              Hermes(MavenPublication) {
                pom {
                    groupId 'com.facebook.hermes'
                    artifactId 'hermes-engine'
                    version hermesVersion
                    packaging 'aar'
                }
              }
            }
          }
        }
        hermesCopy.dependsOn(hermesCopyArchive)
        hermesCopy.dependsOn(generateHermesArchive)
        hermesCopy.dependsOn(generatePomFileForHermesPublication)
    }
    //pack the graphql files in the xnative.aar library so they can be reused by the android app
    task graphQlCopy(type: Copy) {
        from('../../packages/provider-queries/src/')
        include "**/*.graphql"
        into(graphQlFilesPath)
    }

    generateReactArchives.dependsOn(hermesCopy)
    generateReactArchives.dependsOn(reactNativeCopy)
    generateReactArchives.dependsOn(graphQlCopy)
}

model {
    tasks.generatePomFileForHermesPublication {
        destination = file("$reactRepositoryPath/com/facebook/hermes/hermes-engine/$hermesVersion/hermes-engine-${hermesVersion}.pom")
    }
}
