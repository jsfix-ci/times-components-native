apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
import groovy.json.JsonSlurper

def tcnPackageJson = new JsonSlurper().parseText(rootProject.file("../../package.json").text)

def reactRepositoryPath = "$rootDir/repo"

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

project.ext.react = [
  entryFile: "index.js",
  enableHermes: true, // clean and rebuild if changing+
]

publishing {
    publications {
        xnative(MavenPublication) {
            groupId 'uk.co.thetimes'
            artifactId 'times-xnative'
            version tcnPackageJson.version
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/xnative-release.aar")
            pom.withXml {
                final dependenciesNode = asNode().appendNode('dependencies')

                ext.addDependency = { Dependency dep, String scope ->
                    if (dep.group == null || dep.version == null || dep.name == null || dep.name == "unspecified")
                        return // ignore invalid dependencies

                    final dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dep.group)
                    dependencyNode.appendNode('artifactId', dep.name)
                    dependencyNode.appendNode('version', dep.version)
                    dependencyNode.appendNode('scope', scope)

                    if (!dep.transitive) {
                        // If this dependency is transitive, we should force exclude all its dependencies them from the POM
                        final exclusionNode = dependencyNode.appendNode('exclusions').appendNode('exclusion')
                        exclusionNode.appendNode('groupId', '*')
                        exclusionNode.appendNode('artifactId', '*')
                    } else if (!dep.properties.excludeRules.empty) {
                        // Otherwise add specified exclude rules
                        final exclusions = dependencyNode.appendNode('exclusions')
                        dep.properties.excludeRules.each { ExcludeRule rule ->
                            final exclusionNode = exclusions.appendNode('exclusion')
                            exclusionNode.appendNode('groupId', rule.group ?: '*')
                            exclusionNode.appendNode('artifactId', rule.module ?: '*')
                        }
                    }
                }

                // List all "api" dependencies (for new Gradle) as "compile" dependencies
                configurations.api.getAllDependencies().each { dep -> addDependency(dep, "compile") }
                // List all "implementation" dependencies (for new Gradle) as "runtime" dependencies
                configurations.implementation.getAllDependencies().each { dep -> addDependency(dep, "runtime") }
            }
        }
    }
    repositories {
        mavenLocal {
            url "$reactRepositoryPath"
        }
    }
}

android {
//TODO: enable this which is not working to ensure versions are alligned
//              compileSdkVersion rootProject.ext.compileSdkVersion
    compileSdkVersion 31
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName tcnPackageJson.version
        consumerProguardFiles 'proguard-rules.pro'
    }

    packagingOptions {
        exclude "**/libjsc.so"
    }
}

dependencies {
    api "com.facebook.react:react-native:${tcnPackageJson.dependencies.'react-native'}"

    //if (enableHermes)
    implementation("com.facebook.react:hermes-engine:+") {
        exclude group:'com.facebook.fbjni'
    }

    api("uk.co.thetimes:xnative-rn-svg:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
    api("uk.co.thetimes:xnative-rn-webview:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
    api("uk.co.thetimes:xnative-rn-device-info:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
    api("uk.co.thetimes:xnative-rn-community-netinfo:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
    api("uk.co.thetimes:xnative-rn-community-art:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
    api("uk.co.thetimes:xnative-rn-newrelic-agent:${tcnPackageJson.version}") {
        exclude group: "com.facebook.react"
    }
}

gradle.buildFinished { buildResult ->
  println '\n\n\n#### xnative libraries ' + tcnPackageJson.version + ' published to\n\nmaven {\n\turl "' + reactRepositoryPath + '"\n}\n'
  println 'Update your build.gradle/repositories accordingly if you want to fetch local versions\n\n'
}

